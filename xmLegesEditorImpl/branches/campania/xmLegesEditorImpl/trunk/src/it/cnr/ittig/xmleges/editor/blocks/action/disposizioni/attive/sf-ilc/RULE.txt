 /* regola per l'argomento <norma> nelle abrogazioni con soggetto preverbale*/
(1_MOD_NORMA_SOGGPREVERB)
N_C;J_C;X_C*;Y_C
with {
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	!"ABR-NORMA"(Y,Z),
	!"SOST-NORMA"(Y,Z),
	!"INT-NORMA"(Y,Z),
	!"bordo"(Y,N),
	J = N|U,
	Y = FV|BE,
      	Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
  	    IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,
	N.potgov = NORMABR
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {	"ABR-NORMA" (Y,N,plaus=30),
								"ABR-NORMA" (N,J,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,N,plaus=30),
								"INT-NORMA" (N,J,plaus=100)},
		IF (Y.potgov = "sostituire") {	"SOST-NORMA" (Y,N,plaus=30),
						"SOST-NORMA" (N,J,plaus=100)}
		}
	
/* regola per l'argomento <norma> nelle abrogazioni con soggetto postverbale*/
(2_ABR_NORMA_SOGGPOSTVERB)
Y_C;X_C*;N_C;J_C
with {
	X = P|ADVS|ADJ|NA|PNA|di|COORD|SUBORD|RSC|INCPART,
	!"ABR-NORMA"(Y,Z),
	J = N|U,
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere",
  	    IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,
	N.potgov = NORMABR
	} => {
		"ABR-NORMA" (Y,N,plaus=30),
		"ABR-NORMA" (N,J,plaus=100)
		}
	
/* regola per l'argomento <norma> nelle abrogazioni del tipo "all'articolo X, recante ... la parola X è soppressa"*/
(3_MOD_NORMA_AL_RIF)
$P_C;J_C;X_C*;Y_C
with {
	P.potgov = RIF|"articolo",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        J = N|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"inserire"|"aggiungere",
        	IF (Y = FV) {Y.aux = "essere"|"venire"},
       	!"ABR-NORMA"(Y,Z),
       	!"INT-NORMA"(Y,Z)
       	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {	"ABR-NORMA" (Y,P,plaus=30),
								"ABR-NORMA" (P,J,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,P,plaus=30),
								"INT-NORMA" (P,J,plaus=100)}
		}
/* regola per l'argomento <norma> nelle abrogazioni del tipo "all'articolo X, recante ... la parola X è soppressa"*/
(3bis_SOST_NORMA_AL_RIF)
$P_C;J_C;X_C*;Y_C
with {
	P.potgov = RIF|"articolo",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|diNA|I|COORD|SUBORD|N|PUNC|U,
        J = N|U,
        Y = FV|BE,
        Y.potgov = "sostituire",
        	IF (Y = FV) {Y.aux = "essere"|"venire"},
       	!"SOST-NORMA"(Y,Z)
       	} => {
		"SOST-NORMA" (Y,P,plaus=30),
		"SOST-NORMA" (P,J,plaus=100)
		}
	
/* regola per l'argomento <norma> nelle abrogazioni con RIF preverbale, del tipo "il comma X approvato con RIF X, recante ... è abrogato"*/
(4_MOD_NORMA_APPROVATO_PREVERB)
X_C*;ADJPART_C;P_C;J_C;W_C*;Y_C
with {
     	!"ABR-NORMA"(Y,Z),
     	!"SOST-NORMA"(Y,Z),
     	!"INT-NORMA"(Y,Z),
       	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	P.potgov = NORMABR,
       	P.prep = "con",
       	J = N|U,
     	W = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
		IF (Y = FV) {Y.aux = "essere"|"venire"}
       	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {	"ABR-NORMA" (Y,P,plaus=30),
								"ABR-NORMA" (P,J,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,P,plaus=30),
								"INT-NORMA" (P,J,plaus=100)},
		IF (Y.potgov = "sostituire") 		{	"SOST-NORMA" (Y,P,plaus=30),
								"SOST-NORMA" (P,J,plaus=100)}
		}
		
/* regola per l'argomento <norma> nelle abrogazioni con RIF preverbale, del tipo "il comma X del RIF X, recante ... è abrogato"*/
(4bis_MOD_NORMA_PREP_PREVERB)
X_C*;DI_C;J_C;W_C*;Y_C
with {
     	!"ABR-NORMA"(Y,Z),
     	!"SOST-NORMA"(Y,Z),
     	!"INT-NORMA"(Y,Z),
       	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	DI.potgov = NORMABR,
       	J = N|U,
     	W = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
		IF (Y = FV) {Y.aux = "essere"|"venire"}
       	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {	"ABR-NORMA" (Y,DI,plaus=30),
								"ABR-NORMA" (DI,J,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,DI,plaus=30),
								"INT-NORMA" (DI,J,plaus=100)},
		IF (Y.potgov = "sostituire") 		{	"SOST-NORMA" (Y,DI,plaus=30),
								"SOST-NORMA" (DI,J,plaus=100)}
		}		
		
/* regola per l'argomento <norma> nelle modifiche con RIF preverbale, del tipo "il comma X di cui al RIF X, ... è abrogato"*/
(4bisbis_MOD_NORMA_DICUIAL_PREVERB)
X_C*;P_C;R_C;J_C;W_C*;Y_C
with {
       	!"SOST-NORMA"(Y,Z),
       	!"INT-NORMA"(Y,Z),
       	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	P.prep = "di",
       	P.potgov = "cui",
       	R = P,
       	R.prep = "a",
       	R.det != NIL,
       	R.potgov = NORMABR,
       	J = N|U,
     	W = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	Y = FV|BE,
	Y.potgov = "sostituire"|"inserire"|"aggiungere",
		IF (Y = FV) {Y.aux = "essere"|"venire"}
       	} => {
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,R,plaus=30),
								"INT-NORMA" (R,J,plaus=100)},
		IF (Y.potgov = "sostituire") 		{	"SOST-NORMA" (Y,R,plaus=30),
								"SOST-NORMA" (R,J,plaus=100)}
		}			

/* regola per l'argomento <norma> nelle modifiche con RIF preverbale, del tipo "Al numero 4 dell' allegato D annesso al RIF X, e' aggiunto , in fine , il seguente periodo"*/
(4bisbis_MOD_NORMA_ANNESSOAL_PREVERB)
X_C*;ADJPART_C;P_C;J_C;W_C*;Y_C
with {
      	!"SOST-NORMA"(Y,Z),
      	!"INT-NORMA"(Y,Z),
       	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	ADJPART.potgov = "annettere",
       	P.prep = "a",
       	P.potgov = NORMABR,
       	J = N|U,
     	W = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	Y = FV|BE,
	Y.potgov = "sostituire"|"inserire"|"aggiungere",
		IF (Y = FV) {Y.aux = "essere"|"venire"}
       	} => {
		IF (Y.potgov = "inserire"|"aggiungere") {	"INT-NORMA" (Y,P,plaus=30),
								"INT-NORMA" (P,J,plaus=100)},
		IF (Y.potgov = "sostituire") 		{	"SOST-NORMA" (Y,P,plaus=30),
								"SOST-NORMA" (P,J,plaus=100)}
		}				
			

/* regola per l'argomento <norma> nelle abrogazioni con RIF postverbale, del tipo "e' abrogato l' articolo X ... approvato con RIF X"*/
(4ter_MOD_NORMA_APPROVATO_POSTVERB)
Y_C;X_C*;ADJPART_C;P_C;J_C
with {
     	!"ABR-NORMA"(Y,Z),
       	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	P.potgov = NORMABR,
       	P.prep = "con",
       	J = N|U,
     	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere",
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	ADJPART.agr.num = Y.agr.num	
     	} => {
		"ABR-NORMA" (Y,P,plaus=30),
		"ABR-NORMA" (P,J,plaus=100) 					 
		}		
		
/* regola per l'argomento <bordo_tipo> e <bordo_num> nelle abrogazioni del tipo "al RIF X è abrogato il comma 13"*/
(5_MOD_BORDO_TIPO_BORDO_NUM_POSTVERB)
Y_C;N_C;Q_C;PUNC_C*
with {
	!"bordo"(Y,N),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
        N.potgov = BORDOTIPO,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	N.agr.num = Y.agr.num,
        Q = ADJ|N|COORD|LETTERASING|LETTERABIS|LETTERATER,
        	IF (Q = COORD) {Q.conjtype = "e"|"E"|"e-bis"|"E-bis"|"e-ter"|"E-ter"|"o"|"O"|"o-bis"|"O-bis"|"o-ter"|"O-ter"},
        PUNC.punctype = ")"
        } => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		"bordo" (Y,N,plaus=30)
		}
	
/* regola per l'argomento <bordo_tipo> e <bordo_num> nelle abrogazioni del tipo "è abrogato l'articolo X"*/
(5bis_MOD_BORDO_TIPO_BORDO_NUM_POSTVERB)
Y_C;N_C;Q_C;PUNC_C*
with {
	!"bordo"(Y,N),
        N.potgov = BORDOTIPO,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	N.agr.num = Y.agr.num,
        Q = ADJ|N|COORD|LETTERASING|LETTERABIS|LETTERATER,
        	IF (Q = COORD) {Q.conjtype = "e"|"E"|"e-bis"|"E-bis"|"e-ter"|"E-ter"|"o"|"O"|"o-bis"|"O-bis"|"o-ter"|"O-ter"},
        PUNC.punctype = ")"
        } => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		"bordo" (Y,N,plaus=30)
		}

/* regola per l'argomento <bordo_tipo+num> postverbale nelle abrogazioni del tipo "al RIF X è abrogato il terzo comma"*/
(5ter_MOD_BORDO_TIPONUM_POSTVERB)
Y_C;Q_C
with 	{
	!"bordo"(Y,Q),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
	Q.potgov = BORDOTIPO,
	Q = N,
	Q.premodif.cat = A|N,
	Q.premodif != "seguente",
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	Q.agr.num = Y.agr.num
        } => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
	        "bordo" (Y,Q,plaus=30)
		}

/* regola per l'argomento <bordo_tipo> e <bordo_num> nelle abrogazioni del tipo "al RIF X la lettera d) è abrogata"*/
(6_MOD_BORDO_TIPO_BORDO_NUM_PREVERB)
N_C;Q_C;PUNC_C*;X_C*;Y_C
with {
	!"bordo"(Y,N),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
        N.potgov = BORDOTIPO,
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	N.agr.num = Y.agr.num,
	PUNC.punctype = ")",
	Q = ADJ|N|COORD|LETTERASING|LETTERABIS|LETTERATER,
		IF (Q = COORD) {Q.conjtype = "e"|"E"|"e-bis"|"E-bis"|"e-ter"|"E-ter"|"o"|"O"|"o-bis"|"O-bis"|"o-ter"|"O-ter"}
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (N,Q,plaus=100)},
		"bordo" (Y,N,plaus=30)
		}
		
/* regola per l'argomento <bordo_tipo> nelle abrogazioni del tipo "al RIF X la rubrica è abrogata"*/
(6_MOD_BORDO_RUBRICA_PREVERB)
N_C;X_C*;Y_C
with {
	!"bordo"(Y,N),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
        N.potgov = "rubrica",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	N.agr.num = Y.agr.num
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO" (N,N,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO" (N,N,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO" (N,N,plaus=100)},
		"bordo" (Y,N,plaus=30)
		}				
		
/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "al RIF X il secondo comma è abrogato"*/
(6bis_MOD_BORDO_TIPONUM_PREVERB)
Q_C;X_C*;Y_C
with {
	!"bordo"(Y,Q),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
        Q.potgov = BORDOTIPO,
        Q = N,
        Q.premodif.cat = A|N,
        Q.premodif != "seguente",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	Q.agr.num = Y.agr.num
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		"bordo" (Y,Q,plaus=30)
		}		

/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "al RIF X, secondo comma, ... sono soppresse le parole ..." o del tipo "ultimo comma"*/
(6bisbis_MOD_BORDO_TIPONUM_SECONDOCOMMA)
Q_C;X_C*;Y_C
with {
	!"bordo"(Y,Z),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
	Q.potgov = BORDOTIPO,
	Q = P|N,
		IF (Q = P) {P.prep = "secondo"},
		IF (Q = N) {Q.premodif.cat = A|N},
	Q.premodif != "seguente",		
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		"bordo" (Y,Q,plaus=30)
		}			
		
/* regola per l'argomento <bordo_tipo> e <bordo_num> nelle abrogazioni del tipo "all'|dell'|l'articolo X ... sono soppresse le parole X"*/
(7_MOD_AL_BORDO_TIPO_BORDO_NUM_PREVERB)
T_C;Q_C;X_C*;Y_C
with {
       	T.potgov = BORDOTIPO|"testo",
        T = P|DI|N,
	IF (T = P) { T.prep != "secondo" },
       	Q = ADJ|COORD|N,
		IF (Q = N) { Q.potgov = LETS|LETB|LETT },
      		IF (Q = COORD) {Q.conjtype = "e"|"E"|"e-bis"|"E-bis"|"e-ter"|"E-ter"|"o"|"O"|"o-bis"|"O-bis"|"o-ter"|"O-ter"},
      	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (T,Q,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (T,Q,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (T,Q,plaus=100)},
		"bordo" (Y,T,plaus=30)
		}		
		
/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "l'ultimo comma ... è abrogato"*/
(6bter_MOD_BORDO_TIPONUM_PREVERB)
Q_C;X_C*;Y_C
with {
	!"bordo"(Y,Q),
        Q.potgov = BORDOTIPO, 
        Q = P|N,
        	IF (Q = N) {Q.premodif.cat = A|N},
 	Q.premodif != "seguente",       	
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere",
	Q.agr.num = Y.agr.num
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (Q,Q.premodif,plaus=100)},
		"bordo" (Y,Q,plaus=30)
		}			
			
		
/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "al terzo comma ... la lettera X è soppressa "*/
(7bis_MOD_BORDO_TIPONUM_PREVERB)
P_C;X_C*;Y_C
with {
	!"bordo"(Y,P),
        P.potgov = BORDOTIPO,
        P.premodif.cat = A|N,
        P.premodif != "seguente",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (P,P.premodif,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (P,P.premodif,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (P,P.premodif,plaus=100)},
		"bordo" (Y,P,plaus=30)
		}
		
/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "all'articolo X, secondo comma ... la lettera X è soppressa "*/
(7bisbis_MOD_BORDO_SECONDO_PREVERB)
P_C;X_C*;Y_C
with {
	!"bordo"(Y,P),
        P.potgov = BORDOTIPO,
        P.prep = "secondo",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (P,P.prep,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (P,P.prep,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (P,P.prep,plaus=100)},
		"bordo" (Y,P,plaus=30)
		}		
		
/* regola per l'argomento <bordo_tipo> nelle abrogazioni del tipo "all' articolo X, alinea, ... è soppressa la seguente VIR"*/
(7ter_MOD_BORDO_ALINEA_PREVERB)
N_C;X_C*;Y_C
with {
	!"bordo"(Y,N),
	LEFTCONT = P_C;TEMPR*
	  with {
		P.prep = "a"|"in",
		P.potgov = NORMABRPP
	  },
        N.potgov = "alinea",
        X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
        Y = FV|BE,
        Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO" (N,N,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO" (N,N,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO" (N,N,plaus=100)},
		"bordo" (Y,N,plaus=30)
		}					

/* regola per l'argomento <bordo_tipo+num> nelle abrogazioni del tipo "dell'allegato D ... sono soppresse le parole X"*/
(8_MOD_BORDO_TIPO_NUM_ALLEGATO_PREVERB)
T_C;X_C*;Y_C
with {
       	T.premodif = "allegare",
        T = P|DI|N,
      	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
       	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire"|"inserire"|"aggiungere"
	} => {
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-BORDO_TIPO_NUM" (T.premodif,T,plaus=100)},
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-BORDO_TIPO_NUM" (T.premodif,T,plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-BORDO_TIPO_NUM" (T.premodif,T,plaus=100)},
		"bordo" (Y,T,plaus=30)
		}

/* regola per l'argomento <novella> nelle integrazioni e sostituzioni con soggetto postverbale del tipo "è aggiunto il seguente|il seguente comma: VIR X"*/
(9_INT_SOST_NOVELLA_POSTVERB)
Y_C;X_C*;N_C;PUNC_C;Q_C;R_C
with {
	!"novella"(Y,Z),
	Y = FV|BE,
	Y.potgov = "sostituire"|"aggiungere"|"inserire", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.potgov = "seguire"|"comma"|"periodo"|"parola",
		IF (N.potgov = "comma"|"periodo") {N.premodif = "seguente"},
	N.agr.num = Y.agr.num,
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	R = N|U
	 } => {	
	 	"novella" (Y,Q, plaus=30),
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-NOVELLA" (Q,R, plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-NOVELLA" (Q,R, plaus=100)}
		}

/* regola per l'argomento <novella> nelle sostituzioni con soggetto postverbale del tipo "il RIF X è sostituito dal seguente: VIR X"*/
(10_SOST_NOVELLA_POSTVERB)
Y_C;P_C;PUNC_C*;Q_C;R_C
with {
	!"novella"(Y,Z),
	Y = FV|BE,
	Y.potgov = "sostituire", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	P.potgov = "seguire"|"parola",
	P.prep = "da",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	R = N|U
	 } => {	
	 	"novella" (Y,Q, plaus=30),
		"SOST-NOVELLA" (Q,R, plaus=100)
		}


/* regola per l'argomento <novella_tipo> nelle integrazioni e sostituzioni con soggetto postverbale del tipo "è aggiunto il seguente|il seguente comma: VIR X"*/
(11_INT_SOST_NOVELLA_TIPO_POSTVERB)
Y_C;X_C*;N_C
with {
	!"novella_tipo"(Y,Z),
	Y = FV|BE,
	Y.potgov = "sostituire"|"aggiungere"|"inserire", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.potgov = "comma"|"periodo"|"parola",
		IF (N.potgov = "comma"|"periodo") {N.premodif = "seguente"},
	N.agr.num = Y.agr.num,
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U
	 } => {	
	 	"novella_tipo" (Y,N, plaus=30),
		IF (Y.potgov = "inserire"|"aggiungere") {"INT-NOVELLA_TIPO" (N,N, plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-NOVELLA_TIPO" (N,N, plaus=100)}
		}

/* regola per l'argomento <novella_tipo> preverbale nelle integrazioni del tipo "dopo le parole: VIR Y è aggiunto il seguente VIR X"*/	
(12_INT_NOVELLA_TIPO_PREVERB)
SUBORD_C;T_C;PUNC_C;X_C*;Y_C	
with {
	!"novella_tipo"(Y,Z),
	Y = FV|BE,
	Y.potgov = "inserire"|"aggiungere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},	
	SUBORD.conjtype = "dopo"|"prima",
	T = N|di,
	T.potgov = "parola",
	PUNC.punctype = ":",
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U	
	} => {	
	 	"novella_tipo" (Y,T, plaus=30),
	 	"INT-NOVELLA_TIPO" (T,T, plaus=100)
		}
/* regola per l'argomento <posizione> preverbale nelle integrazioni del tipo "dopo le parole: VIR Y è aggiunto il seguente VIR X"*/
(13_INT_POSIZIONE_PREVERB)
PUNC_C;N_C;Q_C;X_C*;Y_C
with {
	!"INT-POSIZIONE"(Y,Z),
	Y = FV|BE,
	Y.potgov = "inserire"|"aggiungere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.potgov = "VIR",
	Q = N|U,
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U	
	} => {	
	 	"INT-POSIZIONE" (Y,N, plaus=30),
	 	"INT-POSIZIONE" (N,Q, plaus=100)
		}

/* regola per l'argomento <novellando> con soggetto preverbale nelle abrogazioni del tipo "All' RIF X, le parole : VIR Y sono soppresse"*/	
(14_ABR_NOVELLANDO_SOGGPREVERB)
N_C;U_C*;PUNC_C;Q_C;T_C;X_C*;Y_C	
with {
	!"novellando"(Y,Z),
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,	
	N.potgov = "parola",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	T = N|U,
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U	
	} => {	
	 	"novellando" (Y,Q, plaus=30),
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-NOVELLANDO" (Q,T, plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-NOVELLANDO" (Q,T, plaus=100)}
		}	
		
/* regola per l'argomento <novellando_tipo> con soggetto preverbale nelle  abrogazioni del tipo "le parole : VIR Y sono soppresse"*/	
(15_ABR_NOVELLANDO_TIPO_SOGGPREVERB)
N_C;U_C*;PUNC_C;Q_C;T_C;X_C*;Y_C	
with {
	!"novellando_tipo"(Y,Z),
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere"|"sostituire", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,			
	N.potgov = "parola",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	T = N|U,
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U	
	} => {	
	 	"novellando_tipo" (Y,N, plaus=30),
		IF (Y.potgov = "abrogare"|"sopprimere") {"ABR-NOVELLANDO_TIPO" (N,N, plaus=100)},
		IF (Y.potgov = "sostituire") 		{"SOST-NOVELLANDO_TIPO" (N,N, plaus=100)}
		}	
		
/* regola per l'argomento <novellando> con soggetto postverbale nelle abrogazioni del tipo "sono soppresse le parole: VIR Y "*/	
(16_ABR_NOVELLANDO_SOGGPOSTVERB)
Y_C;N_C;U_C*;PUNC_C;Q_C;T_C	
with {
	!"novellando"(Y,Z),
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,	
	N.potgov = "parola",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	T = N|U
	} => {	
	 	"novellando" (Y,Q, plaus=30),
	 	"ABR-NOVELLANDO" (Q,T, plaus=100)
		}
		
/* regola per l'argomento <novellando_tipo> con soggetto postverbale nelle abrogazioni del tipo "sono soppresse le parole: VIR Y "*/	
(17_ABR_NOVELLANDO_TIPO_SOGGPOSTVERB)
Y_C;N_C;U_C*;PUNC_C;Q_C;T_C	
with {
	!"novellando_tipo"(Y,Z),
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,	
	N.potgov = "parola",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	T = N|U
	} => {	
	 	"novellando_tipo" (Y,N, plaus=30),
	 	"ABR-NOVELLANDO_TIPO" (N,N, plaus=100)
		}
		
/* regola per l'argomento <ruolo> preverbale nelle abrogazioni del tipo "le parole da: VIR Y fino alla fine del comma sono soppresse"*/	
(18_ABR_RUOLO_DA_PREVERB)
U_C;PUNC_C;X_C*;Y_C	
with {
	!"ruolo"(Y,Z),
	U.potgov = "da",
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	PUNC.punctype = ":",
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U	
	} => {	
	 	"ruolo" (Y,U, plaus=30),
	 	"ABR-NOVELLANDO_RUOLO" (U,U, plaus=100)
		}	
		
/* regola per l'argomento <ruolo> postverbale nelle abrogazioni del tipo "sono soppresse le parole da: VIR Y ..."*/	
(19_ABR_RUOLO_DA_POSTVERB)
Y_C;N_C;U_C;PUNC_C	
with {
	!"ruolo"(Y,Z),
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	N.agr.num = Y.agr.num,	
	N.potgov = "parola",
	PUNC.punctype = ":",
	U.potgov = "da"
	} => {	
	 	"ruolo" (Y,U, plaus=30),
	 	"ABR-NOVELLANDO_RUOLO" (U,U, plaus=100)
		}
		
/* regola per l'argomento <ruolo> postverbale nelle abrogazioni del tipo "sono soppresse le parole da: VIR Y a: VIR Z"*/	
(20_ABR_RUOLO_A_POSTVERB)
Y_C;X_C*;ADV_C;N_C;PUNC_C	
with {
	Y = FV|BE,
	Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
	ADV.potgov = "fino",	
	N.potgov = "a",
	PUNC.punctype = ":",
	X = P|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	"ruolo"(Y,W),
	"novellando"(Y,Q)
	} => {	
	 	"ruolo-bis" (Y,N, plaus=30),
	 	"ABR-NOVELLANDO_RUOLO_BIS" (N,N, plaus=100)
		}					

/* regola per l'argomento <novellando_tipo> con soggetto postverbale nelle abrogazioni del tipo "sono soppresse le parole da: VIR Y a: VIR Z"*/	
(16bis_ABR_NOVELLANDOBIS_TIPO_SOGGPOSTVERB)
N_C;PUNC_C;Q_C;T_C
with {
	LEFTCONT = Y_C;X_C*
	  with {
	  	Y = FV|BE,
		Y.potgov = "abrogare"|"sopprimere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"},
		   X = P|ADV|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|U|PUNC
	  },
	N.potgov = "a",
	PUNC.punctype = ":",
	Q = N,
	Q.potgov = "VIR",
	T = N|U,
	"ruolo-bis"(Y,W),
	"novellando" (Y,Z)
	} => {	
	 	"novellando_bis" (Y,Z, plaus=30),
	 	"ABR-NOVELLANDO_BIS" (Q,T, plaus=100)
		}
		
/* regola per l'argomento <dove> nelle modifiche del tipo "Dopo il RIF X è inserito ..."*/
(20_MOD_DOVE_INIZIALE)
SUBORD_C;X_C+;Y_C
with {
	!"SOST-DOVE"(Y,Z),
	!"INT-DOVE"(Y,Z),
	SUBORD.conjtype = "dopo"|"prima",
	X = P|ADVS|ADJ|ADJPART|NA|PNA|di|COORD|SUBORD|N|PUNC|U,
	Y = FV|BE,
	Y.potgov = "sostituire"|"inserire"|"aggiungere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"}	
	} => {	
	 	IF (Y.potgov = "sostituire") {"SOST-DOVE" (Y,SUBORD, plaus=30),
	 	   	       		      "SOST-DOVE" (SUBORD,SUBORD, plaus=100)},
	 	IF (Y.potgov = "inserire"|"aggiungere") {"INT-DOVE" (Y,SUBORD, plaus=30),
	 	   	       		      		 "INT-DOVE" (SUBORD,SUBORD, plaus=100)}
		}

/* regola per l'argomento <dove> nelle modifiche del tipo "è aggiunto, in fine, il seguente periodo"*/
(21_MOD_DOVE_INFINE)
Y_C;P_C
with {	
	!"SOST-DOVE"(Y,R),
	!"INT-DOVE"(Y,R),
	P.potgov = "fine",
	P.prep = "in",
	Y = FV|BE,
	Y.potgov = "sostituire"|"inserire"|"aggiungere", 
		IF (Y = FV) {Y.aux = "essere"|"venire"}	
	} => {	
	 	IF (Y.potgov = "sostituire") {"SOST-DOVE" (Y,P, plaus=30),
	 	   	       		      "SOST-DOVE" (P,P, plaus=100)},
	 	IF (Y.potgov = "inserire"|"aggiungere") {"INT-DOVE" (Y,P, plaus=30),
	 	   	       		      		 "INT-DOVE" (P,P, plaus=100)}
		}
